unit uSQLBuilder;

interface

uses  System.StrUtils;

type

  TSQLBuilder = class
  strict private
    ST_SELECT : String;
    ST_FROM   : String;
    ST_JOIN   : String;
    ST_WHERE  : String;
    ST_GROUP  : String;
    ST_ORDER  : String;
    ST_HAVING : String;

    function ReadConditions(conditions : array of const) : String;
    function ReadArray(ar : array of const) : String;
    function ContainsComparisonOperator(conditions : array of const) : Boolean;
    function ParentThis( s : String ) : String;
    function PrepareJoin( join, table, alias : string; conditions : array of const ) : TSQLBuilder;
    procedure ResetStructure;
    procedure MountStructure;
    function PrepareWhere( comp : string ; conditions : array of const ; in_list : array of const ) : TSQLBuilder;
  private
    SQL : String;
    function VarRecToStr(AVarRec: TVarRec): string;
  public
    function Select( fields : array of string ; aliases : array of string) : TSQLBuilder; overload;
    function Select( fields : array of string ) : TSQLBuilder; overload;
    function Select( field : String = '*' ) : TSQLBuilder; overload;

    function From( table, alias : String ) : TSQLBuilder; overload;
    function From( table : String ) : TSQLBuilder; overload;

    function Join( table : string ; conditions : array of const ) : TSQLBuilder; overload;
    function Join( table, alias : string ; conditions : array of const ) : TSQLBuilder; overload;
    function InnerJoin( table, alias : string ; conditions : array of const ) : TSQLBuilder; overload;
    function InnerJoin( table : string ; conditions : array of const ) : TSQLBuilder; overload;
    function LeftJoin( table, alias : string ; conditions : array of const ) : TSQLBuilder; overload;
    function LeftJoin( table : string ; conditions : array of const ) : TSQLBuilder; overload;
    function LeftOuterJoin( table, alias : string ; conditions : array of const ) : TSQLBuilder; overload;
    function LeftOuterJoin( table : string ; conditions : array of const ) : TSQLBuilder; overload;
    function RightJoin( table, alias : string ; conditions : array of const ) : TSQLBuilder; overload;
    function RightJoin( table : string ; conditions : array of const ) : TSQLBuilder; overload;
    function RightOuterJoin( table, alias : string ; conditions : array of const ) : TSQLBuilder; overload;
    function RightOuterJoin( table : string ; conditions : array of const ) : TSQLBuilder; overload;
    function FullOuterJoin( table, alias : string ; conditions : array of const ) : TSQLBuilder; overload;
    function FullOuterJoin( table : string ; conditions : array of const ) : TSQLBuilder; overload;
    function CrossJoin( table, alias : string ; conditions : array of const ) : TSQLBuilder; overload;
    function CrossJoin( table : string ; conditions : array of const ) : TSQLBuilder; overload;
    function NaturalJoin( table, alias : string ) : TSQLBuilder; overload;
    function NaturalJoin( table : string ) : TSQLBuilder; overload;
    function JoinRaw( sJoin : String ) : TSQLBuilder; overload;

    function OnIn( field : string ; list: array of const ) : TSQLBuilder;
    function OnNotIn( field : string ; list: array of const ) : TSQLBuilder;
    function OnNull( field : string  ) : TSQLBuilder;
    function OnNotNull( field : string  ) : TSQLBuilder;
    function OnExists( query: string  ) : TSQLBuilder;
    function OnNotExists( query: string  ) : TSQLBuilder;
    function OnBetween( field : string ; two_values : array of const ) : TSQLBuilder;
    function OnNotBetween( field : string ; two_values : array of const ) : TSQLBuilder;

    function Where( conditions : array of const ) : TSQLBuilder;
    function WhereNot( conditions : array of const ) : TSQLBuilder;
    function AndWhere( conditions : array of const ) : TSQLBuilder;
    function AndWhereNot( conditions : array of const ) : TSQLBuilder;
    function OrWhere( conditions : array of const ) : TSQLBuilder;
    function OrWhereNot( conditions : array of const ) : TSQLBuilder;

    function WhereIn( field : string ; list : array of const ) : TSQLBuilder;
    function WhereNotIn( field : string ; list : array of const ) : TSQLBuilder;
    function AndWhereIn( field : string ; list : array of const ) : TSQLBuilder;
    function AndWhereNotIn( field : string ; list : array of const ) : TSQLBuilder;
    function OrWhereIn( field : string ; list : array of const ) : TSQLBuilder;
    function OrWhereNotIn( field : string ; list : array of const ) : TSQLBuilder;

    function WhereNull( field : string ) : TSQLBuilder;
    function WhereNotNull( field : string ) : TSQLBuilder;
    function AndWhereNull( field : string ) : TSQLBuilder;
    function AndWhereNotNull( field : string ) : TSQLBuilder;
    function OrWhereNull( field : string ) : TSQLBuilder;
    function OrWhereNotNull( field : string ) : TSQLBuilder;

    function WhereExists( raw_condition : string ) : TSQLBuilder;
    function WhereNotExists( raw_condition : string ) : TSQLBuilder;
    function AndWhereExists( raw_condition : string ) : TSQLBuilder;
    function AndWhereNotExists( raw_condition : string ) : TSQLBuilder;
    function OrWhereExists( raw_condition : string ) : TSQLBuilder;
    function OrWhereNotExists( raw_condition : string ) : TSQLBuilder;

    function WhereBetween( field : string ; two_values : array of const ) : TSQLBuilder;
    function WhereNotBetween( field : string ; two_values : array of const ) : TSQLBuilder;
    function AndWhereBetween( field : string ; two_values : array of const ) : TSQLBuilder;
    function AndWhereNotBetween( field : string ; two_values : array of const ) : TSQLBuilder;
    function OrWhereBetween( field : string ; two_values : array of const ) : TSQLBuilder;
    function OrWhereNotBetween( field : string ; two_values : array of const ) : TSQLBuilder;

    function WhereRaw( raw_condition : string ) : TSQLBuilder;

    function OrderBy( fields : array of const ) : TSQLBuilder;
    function OrderByRaw( sOrderBy : String ) : TSQLBuilder;

    function Alias( alias : String ) : TSQLBuilder;

    function Get( bParentThis : Boolean = False ) : String;
  end;

function Qt( s : String ) : String;

implementation

uses
  System.SysUtils;

const
  SP = ' ';

{ TSQLBuilderAux }

function TSQLBuilder.AndWhereBetween(field: string;
  two_values: array of const): TSQLBuilder;
begin
  Result := PrepareWhere('and'+SP+field+SP+'between'+SP+
       VarRecToStr(two_values[0])+SP+'and'+SP+VarRecToStr(two_values[1]),[],[]);
end;

function TSQLBuilder.AndWhereNotBetween(field: string;
  two_values: array of const): TSQLBuilder;
begin
  Result := PrepareWhere('and'+SP+field+SP+'not between'+SP+
       VarRecToStr(two_values[0])+SP+'and'+SP+VarRecToStr(two_values[1]),[],[]);
end;

function TSQLBuilder.AndWhere(conditions: array of const): TSQLBuilder;
begin
  Result := PrepareWhere('and',conditions,[]);
end;

function TSQLBuilder.AndWhereExists(raw_condition: string): TSQLBuilder;
begin
  Result := PrepareWhere('and exists'+SP+raw_condition,[],[]);
end;

function TSQLBuilder.AndWhereIn(field: string;
  list: array of const): TSQLBuilder;
begin
  Result := PrepareWhere('and'+SP+field+SP+'in',[],list);
end;

function TSQLBuilder.AndWhereNot(conditions: array of const): TSQLBuilder;
begin
  Result := PrepareWhere('and not',conditions,[]);
end;

function TSQLBuilder.AndWhereNotExists(raw_condition: string): TSQLBuilder;
begin
  Result := PrepareWhere('and not exists'+SP+raw_condition,[],[]);
end;

function TSQLBuilder.AndWhereNotIn(field: string;
  list: array of const): TSQLBuilder;
begin
  Result := PrepareWhere('and'+SP+field+SP+'not in',[],list);
end;

function TSQLBuilder.AndWhereNotNull(field: string): TSQLBuilder;
begin
  Result := PrepareWhere('and'+SP+field+SP+'is not null',[],[]);
end;

function TSQLBuilder.AndWhereNull(field: string): TSQLBuilder;
begin
  Result := PrepareWhere('and'+SP+field+SP+'is null',[],[]);
end;

function TSQLBuilder.ContainsComparisonOperator(conditions: array of const): Boolean;
var
  I: Integer;
  s : String;
const
  CO_1 = '=';
  CO_2 = '>';
  CO_3 = '<';
  CO_4 = '>=';
  CO_5 = '<=';
  CO_6 = '<>';
begin
  Result := False;
  // return if this array has any comparison operator = > <
  for I := 0 to Length(conditions)-1 do
  begin
    s := VarRecToStr(conditions[I]).Trim;

    if MatchStr(s,[CO_1,CO_2,CO_3,CO_4,CO_5,CO_6]) then
    begin
      Result := True;
      Break;
    end;
  end;
end;

function TSQLBuilder.CrossJoin(table, alias: string;
  conditions: array of const): TSQLBuilder;
begin
  Result := PrepareJoin('cross join',table,alias,conditions);
end;

function TSQLBuilder.CrossJoin(table: string;
  conditions: array of const): TSQLBuilder;
begin
  Result := CrossJoin(table,'',conditions);
end;

function TSQLBuilder.From( table, alias : String ) : TSQLBuilder;
var
  sTable : String;
begin
  sTable := table;

  if not alias.IsEmpty then
    sTable := sTable + SP + 'as' + SP + alias;

  ST_FROM := 'from'+SP+sTable;
  ST_JOIN := '';
  MountStructure;
  Result := Self;
end;

function TSQLBuilder.FullOuterJoin(table, alias: string;
  conditions: array of const): TSQLBuilder;
begin
  Result := PrepareJoin('full outer join',table,alias,conditions);
end;

function TSQLBuilder.From(table: String): TSQLBuilder;
begin
  Result := From(table,'');
end;

function TSQLBuilder.FullOuterJoin(table: string;
  conditions: array of const): TSQLBuilder;
begin
  Result := FullOuterJoin(table,'',conditions);
end;

function TSQLBuilder.OnBetween(field: string;
  two_values: array of const): TSQLBuilder;
begin
  ST_JOIN := ST_JOIN+ SP+'and'+SP+
                            field+SP+
                        'between'+SP+
       VarRecToStr(two_values[0])+SP+
                            'and'+SP+VarRecToStr(two_values[1]);

  MountStructure;
  Result := Self;
end;

function TSQLBuilder.OnExists(query: string): TSQLBuilder;
begin
  ST_JOIN := ST_JOIN+SP+'and exists'+SP+ParentThis(query);

  MountStructure;
  Result := Self;
end;

function TSQLBuilder.OnIn(field: string;
  list: array of const): TSQLBuilder;
var
  sCond : String;
begin
  sCond := ReadArray(list);

  ST_JOIN := ST_JOIN+SP+'and'+SP;
  ST_JOIN := ST_JOIN+ParentThis(field+SP+'in'+SP+sCond);

  MountStructure;
  Result := Self;
end;

function TSQLBuilder.OnNotBetween(field: string;
  two_values: array of const): TSQLBuilder;
begin
  ST_JOIN := ST_JOIN+ SP+'and'+SP+
                            field+SP+
                    'not between'+SP+
       VarRecToStr(two_values[0])+SP+
                            'and'+SP+VarRecToStr(two_values[1]);

  MountStructure;
  Result := Self;
end;

function TSQLBuilder.OnNotExists(query: string): TSQLBuilder;
begin
  ST_JOIN := ST_JOIN+SP+'and not exists'+SP+ParentThis(query);

  MountStructure;
  Result := Self;
end;

function TSQLBuilder.OnNotIn(field: string; list: array of const): TSQLBuilder;
var
  sCond : String;
begin
  sCond := ReadArray(list);

  ST_JOIN := ST_JOIN+SP+'and'+SP;
  ST_JOIN := ST_JOIN+ParentThis(field+SP+'not in'+SP+sCond);

  MountStructure;
  Result := Self;
end;

function TSQLBuilder.OnNotNull(field: string): TSQLBuilder;
begin
  ST_JOIN := ST_JOIN+SP+'and'+SP+field+SP+'is not null';

  MountStructure;
  Result := Self;
end;

function TSQLBuilder.OnNull(field: string): TSQLBuilder;
begin
  ST_JOIN := ST_JOIN+SP+'and'+SP+field+SP+'is null';

  MountStructure;
  Result := Self;
end;

function TSQLBuilder.OrWhereBetween(field: string;
  two_values: array of const): TSQLBuilder;
begin
  Result := PrepareWhere('or'+SP+field+SP+'between'+SP+
       VarRecToStr(two_values[0])+SP+'and'+SP+VarRecToStr(two_values[1]),[],[]);
end;

function TSQLBuilder.OrderBy(fields: array of const): TSQLBuilder;
var
  I: Integer;
  sOrderBy, sVal : String;
const
  DESC = 'desc';
begin
  sOrderBy := '';
  for I := 0 to Length(fields)-1 do
  begin
    sVal := VarRecToStr(fields[i]).Trim;
    if (not (sOrderBy = '')) and (not (sVal.ToLower=DESC)) then
      sOrderBy := sOrderBy + ',';
    sOrderBy := sOrderBy +SP+ sVal;
  end;
  ST_ORDER := 'order by'+sOrderBy;
  MountStructure;
  Result := Self;
end;

function TSQLBuilder.OrderByRaw( sOrderBy : String ) : TSQLBuilder;
begin
  ST_ORDER := 'order by'+SP+sOrderBy;
  MountStructure;
  Result := Self;
end;

function TSQLBuilder.OrWhereNotBetween(field: string;
  two_values: array of const): TSQLBuilder;
begin
  Result := PrepareWhere('or'+SP+field+SP+'not between'+SP+
       VarRecToStr(two_values[0])+SP+'and'+SP+VarRecToStr(two_values[1]),[],[]);
end;

function TSQLBuilder.OrWhere(conditions: array of const): TSQLBuilder;
begin
  Result := PrepareWhere('or',conditions,[]);
end;

function TSQLBuilder.OrWhereExists(raw_condition: string): TSQLBuilder;
begin
  Result := PrepareWhere('or exists'+SP+raw_condition,[],[]);
end;

function TSQLBuilder.OrWhereIn(field: string;
  list: array of const): TSQLBuilder;
begin
  Result := PrepareWhere('or'+SP+field+SP+'in',[],list);
end;

function TSQLBuilder.OrWhereNot(conditions: array of const): TSQLBuilder;
begin
  Result := PrepareWhere('or not',conditions,[]);
end;

function TSQLBuilder.OrWhereNotExists(raw_condition: string): TSQLBuilder;
begin
  Result := PrepareWhere('or not exists'+SP+raw_condition,[],[]);
end;

function TSQLBuilder.OrWhereNotIn(field: string;
  list: array of const): TSQLBuilder;
begin
  Result := PrepareWhere('or'+SP+field+SP+'not in',[],list);
end;

function TSQLBuilder.OrWhereNotNull(field: string): TSQLBuilder;
begin
  Result := PrepareWhere('or'+SP+field+SP+'is not null',[],[]);
end;

function TSQLBuilder.OrWhereNull(field: string): TSQLBuilder;
begin
  Result := PrepareWhere('or'+SP+field+SP+'is null',[],[]);
end;

function TSQLBuilder.ParentThis(s: String): String;
begin
  Result := '(' + s + ')';
end;

function TSQLBuilder.PrepareJoin( join, table, alias : string; conditions : array of const ) : TSQLBuilder;
var
  s, sTable : String;
begin
  sTable := table;

  if not alias.IsEmpty then
    sTable := sTable + SP + 'as' + SP + alias;

  if not ST_JOIN.IsEmpty then
    ST_JOIN := ST_JOIN + #13#10;

  if Length(conditions) > 0 then
    s := join+SP+sTable+SP+'on'+SP+ReadConditions(conditions)
  else
    s := join+SP+sTable;

  ST_JOIN := ST_JOIN + s;
  MountStructure;
  Result := Self;
end;

function TSQLBuilder.PrepareWhere( comp : string ; conditions : array of const ; in_list : array of const ) : TSQLBuilder;
var
  sCond : String;
begin
  sCond := '';
  if not (Length(conditions)=0) then
    sCond := ReadConditions(conditions);

  if not (Length(in_list)=0) then
    sCond := ReadArray(in_list);

  if ST_WHERE.IsEmpty then
    ST_WHERE := comp+SP+sCond
  else
    ST_WHERE := ST_WHERE+#13#10+comp+SP+sCond;

  MountStructure;
  Result := Self;
end;

function TSQLBuilder.ReadArray(ar: array of const): String;
var
  I: Integer;
begin
  Result := '';
  for I := 0 to Length(ar)-1 do
  begin
    if not Result.IsEmpty then
      Result := Result +',';
    Result := Result + VarRecToStr(ar[I]);
  end;
  Result := ParentThis(Result);
end;

function TSQLBuilder.ReadConditions(conditions: array of const): String;
var
  X, I, Z: Integer;
begin
  Result := '';
  if Length(conditions) = 2 then
  begin
    Result := ParentThis(VarRecToStr(conditions[0]) +SP+ '=' +SP+ VarRecToStr(conditions[1]));
  end
  else if ((Length(conditions) mod 2) = 0) and (not ContainsComparisonOperator(conditions)) then
  begin
    X := Trunc(Length(conditions)/2);
    for I := 0 to X-1 do
    begin
      if not Result.IsEmpty then
        Result := Result +SP+ 'and' +SP;

      Z := 2*I;

      Result := Result + ReadConditions([ VarRecToStr(conditions[Z]),
                                          VarRecToStr(conditions[Z+1]) ]);
    end;
    Result := Result;
  end
  else if ((Length(conditions) mod 3) = 0) and (Length(conditions)<>3) then
  begin
    X := Trunc(Length(conditions)/3);
    for I := 0 to X-1 do
    begin
      if not Result.IsEmpty then
        Result := Result +SP+ 'and' +SP;

      Z := 3*I;

      Result := Result + ReadConditions([ VarRecToStr(conditions[Z]),
                                          VarRecToStr(conditions[Z+1]),
                                          VarRecToStr(conditions[Z+2]) ]);
    end;
    Result := Result;
  end
  else
  begin
    for I := 0 to Length(conditions)-1 do
    begin
      if not (Result = '') then
        Result := Result+SP;
      Result := Result+VarRecToStr( conditions[i] );
    end;
    Result := ParentThis(Result);
  end;
end;

function TSQLBuilder.Alias( alias : String ) : TSQLBuilder;
begin
  ST_FROM := ST_FROM+SP+'as'+SP+alias;
  MountStructure;
  Result := Self;
end;

function TSQLBuilder.Get( bParentThis : Boolean = False ) : String;
begin
  MountStructure;
  if bParentThis then
    SQL := ParentThis(SQL);
  Result := SQL;
  ResetStructure;
end;

function TSQLBuilder.InnerJoin(table, alias: string;
  conditions: array of const): TSQLBuilder;
begin
  Result := PrepareJoin('inner join',table,alias,conditions);
end;

function TSQLBuilder.InnerJoin(table: string;
  conditions: array of const): TSQLBuilder;
begin
  Result := InnerJoin(table,'',conditions);
end;

function TSQLBuilder.Join(table, alias: string;
  conditions: array of const): TSQLBuilder;
begin
  Result := PrepareJoin('join',table,alias,conditions);
end;

function TSQLBuilder.JoinRaw(sJoin: String): TSQLBuilder;
begin
  ST_JOIN := sJoin;
  MountStructure;
  Result := Self;
end;

function TSQLBuilder.LeftJoin(table, alias: string;
  conditions: array of const): TSQLBuilder;
begin
  Result := PrepareJoin('left join',table,alias,conditions);
end;

function TSQLBuilder.LeftJoin(table: string;
  conditions: array of const): TSQLBuilder;
begin
  Result := LeftJoin(table,'',conditions);
end;

function TSQLBuilder.LeftOuterJoin(table, alias: string;
  conditions: array of const): TSQLBuilder;
begin
  Result := PrepareJoin('left outer join',table,alias,conditions);
end;

function TSQLBuilder.LeftOuterJoin(table: string;
  conditions: array of const): TSQLBuilder;
begin
  Result := LeftOuterJoin(table,'',conditions);
end;

function TSQLBuilder.Join( table : string ; conditions : array of const ) : TSQLBuilder;
begin
  Result := Join(table,'',conditions);
end;

procedure TSQLBuilder.MountStructure;
begin
  SQL := ST_SELECT +#13#10+
         ST_FROM;

  if not ST_JOIN.IsEmpty then
    SQL := SQL + #13#10 + ST_JOIN;

  if not ST_WHERE.IsEmpty then
    SQL := SQL + #13#10 + ST_WHERE;

  if not ST_GROUP.IsEmpty then
    SQL := SQL + #13#10 + ST_GROUP;

  if not ST_HAVING.IsEmpty then
    SQL := SQL + #13#10 + ST_HAVING;

  if not ST_ORDER.IsEmpty then
    SQL := SQL + #13#10 + ST_ORDER;
end;

function TSQLBuilder.NaturalJoin(table, alias: string): TSQLBuilder;
begin
  Result := PrepareJoin('natural join',table,alias,[]);
end;

function TSQLBuilder.NaturalJoin(table: string): TSQLBuilder;
begin
  Result := NaturalJoin(table,'');
end;

function TSQLBuilder.Where( conditions : array of const ) : TSQLBuilder;
begin
  Result := PrepareWhere('where',conditions,[]);
end;

function TSQLBuilder.WhereBetween(field: string;
  two_values: array of const): TSQLBuilder;
begin
  Result := PrepareWhere('where' +SP+field+SP+'between'+SP+
       VarRecToStr(two_values[0])+SP+'and'+SP+VarRecToStr(two_values[1]),[],[]);
end;

function TSQLBuilder.WhereExists(raw_condition: string): TSQLBuilder;
begin
  Result := PrepareWhere('where exists'+SP+raw_condition,[],[]);
end;

function TSQLBuilder.WhereIn( field : string ; list : array of const ) : TSQLBuilder;
begin
  Result := PrepareWhere('where'+SP+field+SP+'in',[],list);
end;

function TSQLBuilder.WhereNot(conditions: array of const): TSQLBuilder;
begin
  Result := PrepareWhere('where not',conditions,[]);
end;

function TSQLBuilder.WhereNotBetween(field: string;
  two_values: array of const): TSQLBuilder;
begin
  Result := PrepareWhere('where' +SP+field+SP+'not between'+SP+
       VarRecToStr(two_values[0])+SP+'and'+SP+VarRecToStr(two_values[1]),[],[]);
end;

function TSQLBuilder.WhereNotExists(raw_condition: string): TSQLBuilder;
begin
  Result := PrepareWhere('where not exists'+SP+raw_condition,[],[]);
end;

function TSQLBuilder.WhereNotIn( field : string ; list : array of const ) : TSQLBuilder;
begin
  Result := PrepareWhere('where'+SP+field+SP+'not in',[],list);
end;

function TSQLBuilder.WhereNotNull(field: string): TSQLBuilder;
begin
  Result := PrepareWhere('where'+SP+field+SP+'is not null',[],[]);
end;

function TSQLBuilder.WhereNull(field: string): TSQLBuilder;
begin
  Result := PrepareWhere('where'+SP+field+SP+'is null',[],[]);
end;

function TSQLBuilder.WhereRaw(raw_condition: string): TSQLBuilder;
begin

end;

{ TSQLBuilder }

procedure TSQLBuilder.ResetStructure;
begin
  ST_SELECT := '';
  ST_FROM   := '';
  ST_WHERE  := '';
  ST_GROUP  := '';
  ST_ORDER  := '';
  ST_HAVING := '';
  SQL := '';
end;

function TSQLBuilder.RightJoin(table, alias: string;
  conditions: array of const): TSQLBuilder;
begin
  Result := PrepareJoin('right join',table,alias,conditions);
end;

function TSQLBuilder.RightJoin(table: string;
  conditions: array of const): TSQLBuilder;
begin
  Result := RightJoin(table,'',conditions);
end;

function TSQLBuilder.RightOuterJoin(table, alias: string;
  conditions: array of const): TSQLBuilder;
begin
  Result := PrepareJoin('right outer join',table,alias,conditions);
end;

function TSQLBuilder.RightOuterJoin(table: string;
  conditions: array of const): TSQLBuilder;
begin
  Result := RightOuterJoin(table,'',conditions);
end;

function TSQLBuilder.Select(fields: array of string ; aliases : array of string): TSQLBuilder;
var
  I: Integer;
  sFields : String;
begin
  sFields := '';
  for I := 0 to Length(fields)-1 do
  begin
    if not (sFields = '') then
      sFields := sFields + ',';

    if (Length(aliases) < I ) then
      sFields := sFields + SP + fields[I]
    else
    begin
      if not aliases[I].IsEmpty then
        sFields := sFields + SP + fields[I] + SP + 'as' + SP + aliases[I]
      else
        sFields := sFields + SP + fields[I];
    end;
  end;
  ST_SELECT := 'select'+sFields;
  MountStructure;
  Result := Self;
end;

function TSQLBuilder.Select(field: String = '*'): TSQLBuilder;
begin
  ST_SELECT := 'select'+SP+field;
  MountStructure;
  Result := Self;
end;

function TSQLBuilder.Select(fields: array of string): TSQLBuilder;
begin
  Result := Select(fields,['']);
end;

function TSQLBuilder.VarRecToStr( AVarRec : TVarRec ) : string;
const
  Bool : array[Boolean] of string = ('False', 'True');
begin
  case AVarRec.VType of
    vtInteger:    Result := IntToStr(AVarRec.VInteger);
    vtBoolean:    Result := Bool[AVarRec.VBoolean];
    vtChar:       Result := AVarRec.VChar;
    vtExtended:   Result := FloatToStr(AVarRec.VExtended^);
    vtString:     Result := AVarRec.VString^;
    vtPChar:      Result := AVarRec.VPChar;
    vtObject:     Result := AVarRec.VObject.ClassName;
    vtClass:      Result := AVarRec.VClass.ClassName;
    vtWideChar:   Result := AVarRec.VWideChar;
    vtAnsiString: Result := string(AVarRec.VAnsiString);
    vtCurrency:   Result := CurrToStr(AVarRec.VCurrency^);
    vtVariant:    Result := string(AVarRec.VVariant^);
    vtUnicodeString: Result := String(AVarRec.VUnicodeString);

  else
    result := '';
  end;
end;

function Qt( s : String ) : String;
begin
  Result := QuotedStr(s);
end;

end.
